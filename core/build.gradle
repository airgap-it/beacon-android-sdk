plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlinx-serialization'
    id 'org.jetbrains.dokka'
    id 'maven-publish'
}

android {
    compileSdk Android.compileSdk

    defaultConfig {
        minSdk Android.minSdk
        targetSdk Android.targetSdk
        versionCode Android.versionCode
        versionName Android.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    defaultPublishConfig "prodRelease"

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"
    productFlavors {
        prod {
            dimension "version"
            buildConfigField("String", "VERSION_NAME", "\"${defaultConfig.versionName}\"")

        }
        mock {
            dimension "version"
            buildConfigField("String", "VERSION_NAME", "\"${defaultConfig.versionName}-mock\"")
        }
    }

    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += '-Xexplicit-api=warning'
        freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    afterEvaluate {
        publishing {
            publications {
                release(MavenPublication) {
                    from components.prodRelease

                    groupId = 'it.airgap.beaconsdk'
                    artifactId = 'core'
                    version = Android.versionName
                }
            }
        }
    }
}

dependencies {
    implementation(Dependencies.kotlinStdlib)

    implementation(Dependencies.androidxCore)
    implementation(Dependencies.androidxAppCompat)

    // Security
    implementation(Dependencies.androidxSecurity)

    // Reflection
    implementation(Dependencies.kotlinReflection)

    // Serialization
    implementation(Dependencies.kotlinxSerializationJson)

    // Coroutines
    implementation(Dependencies.kotlinxCoroutines)
    implementation(Dependencies.kotlinxCoroutinesAndroid)

    // Ktor
    implementation(Dependencies.ktorOkHttp)
    implementation(Dependencies.ktorJson)
    implementation(Dependencies.ktorSerializationJvm)
    implementation(Dependencies.ktorLoggingJvm)

    // Lazy Sodium
    implementation(Dependencies.lazySodium) {
        exclude group: 'net.java.dev.jna'
    }

    // Test
    testImplementation(TestDependencies.kotlinTest)
    testImplementation(TestDependencies.kotlinxCoroutinesTest)

    testImplementation(TestDependencies.junit)
    testImplementation(TestDependencies.mockk)

    androidTestImplementation(TestDependencies.androidxJunit)
    androidTestImplementation(TestDependencies.androidxEspresso)
}

dokkaHtml.configure {
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
        }
    }
}