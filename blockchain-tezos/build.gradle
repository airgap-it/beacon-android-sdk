import it.airgap.beaconsdk.build.Android
import it.airgap.beaconsdk.build.Dependencies
import it.airgap.beaconsdk.build.TestDependencies

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlinx-serialization'
    id 'org.jetbrains.dokka'
    id 'maven-publish'
}

android {
    namespace "${Android.namespace}.blockchain.tezos"
    compileSdk Android.compileSdk

    defaultConfig {
        minSdk Android.minSdk
        version Android.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        buildConfig true
    }

    flavorDimensions "${Android.namespace}.version"
    productFlavors {
        prod {
            dimension "${Android.namespace}.version"
            buildConfigField("String", "VERSION_NAME", "\"${defaultConfig.versionName}\"")

        }
        mock {
            dimension "${Android.namespace}.version"
            buildConfigField("String", "VERSION_NAME", "\"${defaultConfig.versionName}-mock\"")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += '-Xexplicit-api=warning'
        freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'
    }

    publishing {
        singleVariant("prodRelease") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.prodRelease

                groupId = 'it.airgap.beaconsdk'
                artifactId = 'blockchain-tezos'
                version = Android.versionName
            }
        }
    }
}

dependencies {
    implementation project(path: ':core')

    implementation(Dependencies.kotlinStdlib)

    implementation(Dependencies.androidxCore)
    implementation(Dependencies.androidxAppCompat)

    // Serialization
    implementation(Dependencies.kotlinxSerializationJson)

    // Test
    testImplementation(TestDependencies.kotlinTest)
    testImplementation(TestDependencies.kotlinxCoroutinesTest)

    testImplementation(TestDependencies.junit)
    testImplementation(TestDependencies.mockk)

    androidTestImplementation(TestDependencies.androidxJunit)
    androidTestImplementation(TestDependencies.androidxEspresso)

    testImplementation(TestDependencies.bouncyCastle)
}

dokkaHtml.configure {
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
        }
    }
}